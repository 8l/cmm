% -*- mode: Noweb; noweb-code-mode: caml-mode -*-

% ------------------------------------------------------------------ 
% Grades     ::=    "%%Grades:" Quality Importance Urgency
% Quality    ::=    A|B|C|D|E
% Importance ::=    Central|Subsystem|Peripheral
% Urgency    ::=    Immediate|Soon|Later
%
% Example (at beginning of line): %%Grades: B Central Soon
\section{Relocatable Addresses}
% ------------------------------------------------------------------ 

A relocatable address formed by sums and differences of symbolic and
literal constants.
A [['a relocatable]] value represents a bit vector of type [[['a]] whose
value might not be known until link time. Since the representation is 
polymorphic over the type of the constant, [[add_const]] receives as
first argument a function that adds constants.

It might be necessary to turn a relocatable address back into an
expression, which might conceivably undergo a PIC transformation.
For that reason, to make a relocatable address from a symbol, it is
necessary to supply a suitable function.
<<reloc.mli>>=
type symbol = Symbol.t * (Symbol.t -> Rtl.width -> Rtl.exp)
type t

(* constructors *)
val of_const : Bits.bits -> t
val of_sym   : symbol -> Rtl.width -> t
val add :  t -> t -> t
val sub :  t -> t -> t

(* observers *)
val fold : const:(Bits.bits -> 'a) -> sym:(symbol -> 'a) ->
           add:('a -> 'a -> 'a) -> sub:('a -> 'a -> 'a) -> t -> 'a

val width : t -> Rtl.width
val if_bare : t -> Bits.bits option (* if not a bare value, returns None *)
val as_simple : t -> Symbol.t option * Bits.bits
   (* checked RTE if not simple *)
@
% ------------------------------------------------------------------ 
\subsection{Implementation}
% ------------------------------------------------------------------ 

No known need for efficiency here.
<<reloc.ml>>=
type symbol = Symbol.t * (Symbol.t -> Rtl.width -> Rtl.exp)
let eqsym (s,_) (s',_) = s#mangled_text =$= s'#mangled_text

type exp = Pos of symbol * Rtl.width | Neg of symbol * Rtl.width
let neg = function Pos (s, w) -> Neg (s, w) | Neg (s, w) -> Pos (s, w)

type t  = exp list * Bits.bits
let of_const c    = ([], c)
let of_sym s w    = ([Pos (s, w)], Bits.zero w)
let add (xs, xc) (ys, yc) = (xs @ ys, Bits.Ops.add xc yc)
let sub (xs, xc) (ys, yc) = (xs @ List.map neg ys, Bits.Ops.sub xc yc)
let fold ~const ~sym ~add ~sub (ss, c) =
  let extend e = function Pos (s, w) -> add e (sym s)
                        | Neg (s, w) -> sub e (sym s) in
  match ss with
  | Pos (s, w) :: ss when Bits.is_zero c -> List.fold_left extend (sym s)   ss
  | _                                    -> List.fold_left extend (const c) ss
@
<<reloc.ml>>=
let width (_, b) = Bits.width b
let if_bare = function ([], b) -> Some b | (_::_, _) -> None
let as_simple a =
  let w = Bits.width (snd a) in
  let const bits = (None, bits) in
  let sym (s, _) = (Some s, Bits.zero w) in
  let add (s, b) (s', b') = match s, s' with
  | Some s, None   -> (Some s, Bits.Ops.add b b')
  | None, Some s   -> (Some s, Bits.Ops.add b b')
  | None, None     -> (None, Bits.Ops.add b b')
  | Some _, Some _ -> Impossible.impossible "added symbols in simple reloc" in
  let sub (s, b) (s', b') = match s' with
  | None -> (s, Bits.Ops.sub b b')
  | Some _ -> Impossible.impossible "subtracted symbols in simple reloc" in
  fold ~const ~sym ~add ~sub a
