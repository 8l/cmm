% -*- mode: Noweb; noweb-code-mode: caml-mode -*-
% vim: sw=4 ts=8 et:

% Grades     ::=    "%%Grades:" Quality Importance Urgency
% Quality    ::=    A|B|C|D|E
% Importance ::=    Central|Subsystem|Peripheral
% Urgency    ::=    Immediate|Soon|Later
%
% Please add grades for this module below. The line must start with % "%%".
% 

%%Grades: B Peripheral Later

%
% Todo: define rounding mode register (names: fp_mode) in space 'c';
% extend 'location' location type. Since 'c' is 32 bits wide, but the
% rounding mode only 2 bits wide, we take slices: RP.slice (2, 0,
% fp_mode) when using it.
%


% ------------------------------------------------------------------ 
\section{RTL-Spaces}
% ------------------------------------------------------------------ 

The {\rtl} framework groups memory cells (main memory, registers,
temporaries) into named spaces. The cells forming a space share
properties but the {\rtl} framework does not announce them. This module
provides a type to describe an {\rtl} space. 

The function [[checked]] asserts that its argument satisfies a few
simple invariants, then returns that argument.
<<space.mli>>=
<<definitions of exported types, including [[t]]>>
val checked : t -> t
<<definition of signature [[Standard]]>>
module Standard32 : Standard
module Standard64 : Standard
@
The module [[Standard]] provides support for common spaces with
standard names.  Memory uses 8-bit cells, and other units use 32-bit
cells.  
Byte order and available aggregate widths must be specified by the
client. 
As of October 2004, temporary registers cannot be aggregated; the byte
order specified for 
a temporary space is the byte order of the underlying hardware space,
and exactly one width is supported.
<<definition of signature [[Standard]]>>=
module type Standard = sig
  type generator  = Rtl.aggregation -> Rtl.width list -> t
  type tgenerator = Rtl.aggregation -> Rtl.width      -> t
  val m   :               generator          (* standard 8-bit memory *)
  val r   :  count:int -> generator          (* registers *)
  val t   :               tgenerator         (* register temps *)
  val f   :  count:int -> generator          (* floats *)
  val u   :               tgenerator         (* float temps *)
  val a   :  count:int -> generator          (* address registers *)
  val v   :               tgenerator         (* address temps *)
  val p   :  count:int -> generator          (* predicate registers *)
  val w   :               tgenerator         (* predicate temps *)
  val c   :  count:int -> generator          (* control and special registers *)
  val vf  :               generator          (* virtual frame pointer *)
  val x   :               generator          (* boxed rtls *)
  val s   :  int       -> tgenerator         (* stack-slots temporaries *)
  
  type 'a locations = 
    { pc:       'a
    ; npc:      'a
    ; cc:       'a
    ; fp_mode:  'a  (* FP rounding mode   *)
    ; fp_fcmp:  'a  (* FP %fcmp results   *)
    }

  val locations : c:t -> Rtl.loc locations
        (* apply to c space to get standard locations *)
  val indices : int locations (* standard indices in c space *)
  val vfp : Rtl.exp  (* the virtual frame pointer, $V[0] *)
end
@ 
To define spaces, we must first define [[classification]] and
[[location_set]].
A [[Rtl.space]] denotes a memory space, the details of a particular
space are covered by a space descriptor [[t]].
<<definitions of exported types, including [[t]]>>=
<<definition of type [[location_set]]>>
<<definition of type [[classification]]>>
type t  =
    { space:            Rtl.space       (* space being described *)
    ; doc:              string          (* informal doc string *)       
    ; indexwidth:       int             (* bits *)
    ; indexlimit:       int option      (* None = 2 ** indexwidth *)
    ; widths:           int list        (* bit widths of supported aggregates *)
    ; classification:   classification  
    }
@
\begin{itemize}
\item [[doc]]:          Informal documentation string.
\item [[indexwidth]]:   Width of an address in this space. A memory
                        space could have 32 bit wide addresses, a
                        register space 5 bit wide addresses.
\item [[indexlimit]]:   Upper bound for an address in this space: $0,
                        \dots, [[indexlimit]]$. 
\item [[widths]]:       Widths of supported aggregates. For example, 
                        if an  memory space 8~bits wide supports 8-, 16-, and
                        32-bit words, [[widths]] would be [[[8;16;32]]].
\item [[classification]]: Classification of a space, see below.
\end{itemize}
@
A space falls into one of the following classifications: memory-like,
register-like, fixed, or temporary. 
<<definition of type [[classification]]>>=
type classification = 
    | Mem               
    | Reg               
    | Fixed
    | Temp of location_set
@ Memory-like, register-like, and fixed spaces contain real hardware
locations.
A~temporary space contains only locations that stand in for hardware
locations.
All temporary locations are removed before code generation is
complete. 
(This is the job of the register allocator.)
@
The classification of a space depends on the binding time of the
expressions used to index cells in that space.
\begin{itemize}
\item
 If an {\rtl} operand addressing the space denotes a value that
      cannot be computed until run time, the space is memory-like.
      An example of such an expression is $\mathit{SP}+12$.
      The standard \texttt{m}~space is memory-like.
\item
 If every {\rtl} operand addressing the space is a compile-time
 constant, and if the constant can ever be chosen by the compiler,
 then the space is register-like. 
 (In machine-language terms, a space is register-like if the values of its
 indexing expressions can be computed from the operands of the
 instructions that use the space.)
 Most standard spaces are register-like, including \texttt{r},
 \texttt{f}, and~\texttt{a}.
\item
 If every {\rtl} operand addressing the space is a fixed
 constant determined by the nature of the instruction, with no choice
 available to the compiler,
 then the space is fixed. 
 (In machine-language terms, a space is fixed if the values of its
 indexing expressions are determined by the instructions that use the
 space, and the operands of those instructions are irrelevant.)
 The standard \texttt{c}~space is fixed.
\item
   A temporary space is a register-like space that is infinitely large
   and not provided by the hardware.
   A~location in a temporary space denotes one of a set of locations
   drawn from a real register-like space.
   The set is
      characterized by a [[location_set]] value. Example: on the
   SPARC, the \texttt{t}~space is
      a space over \texttt{r}, where the address of an \texttt{r} cell
      is not zero.  
   The register allocator ultimately replaces each temporary location
   with a real hardware location.
\end{itemize}
<<definition of type [[location_set]]>>=
type location_set =
    { stands_for : Register.t -> bool  (* what registers we stand for *)
    ; set_doc    : string     (* informal description *)    
    }
@ We make it easy to build a simple predicate.
<<space.mli>>=
val stands_for : char -> Register.aggregation -> Register.width -> (Register.t -> bool)
@    


% ------------------------------------------------------------------ 
\subsection{Implementation}
% ------------------------------------------------------------------ 

<<space.ml>>=
<<definitions of exported types, including [[t]]>>
<<definition of signature [[Standard]]>>
@

<<space.ml>>=
let indexwidth n =
  let rec wid = function
    | 0 -> 0
    | n -> 1 + wid (n lsr 1)  in
  wid (n-1)
let checked s =
  begin
    let (_, agg, cell) = s.space in
    assert (match s.widths with [] -> false | _ :: _ -> true);
    List.iter (fun w -> assert (Cell.divides cell w)) s.widths;
    (match agg with
    | Rtl.Identity -> assert(s.widths =*= [Cell.to_width cell (Cell.C 1)])
    | _ -> ());
    (match s.indexlimit with
    | Some n -> assert (indexwidth n <= s.indexwidth)
    | _ -> ());
    s
  end
let stands_for s agg w =
  (fun ((s', agg', c), _, Register.C ct) ->
    s' =<= s && agg =*= agg' && ct = 1 && Cell.size c = w)
@

A set of standard spaces can now be constructed from a simple functor
argument. Maybe we need to extend the argument signature for 64-bit
architectures.

<<space.ml>>=
module Standard(A:sig val width: int end) = struct
  type generator  = Rtl.aggregation -> Rtl.width list -> t
  type tgenerator = Rtl.aggregation -> Rtl.width      -> t
  let m agg ws = checked 
    { space          = ('m', agg, Cell.of_size 8)
    ; doc            = "memory"
    ; indexwidth     = A.width 
    ; indexlimit     = None
    ; widths         = ws
    ; classification = Mem
    } 

  let r ~count agg ws = checked 
    { space          = ('r', agg, Cell.of_size A.width)
    ; doc            = "general-purpose registers"
    ; indexwidth     = indexwidth count
    ; indexlimit     = Some count
    ; widths         = ws
    ; classification = Reg
    } 
  let f ~count agg ws = checked 
    { space         = ('f', agg, Cell.of_size A.width)
    ; doc           = "floating-point registers"
    ; indexwidth    = indexwidth count
    ; indexlimit    = Some count
    ; widths        = ws
    ; classification = Reg
    }
    
  let a ~count agg ws = checked 
    { space         = ('a', agg, Cell.of_size A.width)
    ; doc           = "address registers"
    ; indexwidth    = indexwidth count
    ; indexlimit    = Some count
    ; widths        = ws
    ; classification = Reg
    } 
  
  let p ~count agg ws = checked 
    { space          = ('p', agg, Cell.of_size 1)
    ; doc            = "predicate registers"
    ; indexwidth     = indexwidth count
    ; indexlimit     = Some count
    ; classification = Reg
    ; widths         = ws
    }
    

    (* CHECK THAT count IS LARGE ENOUGH TO COVER indicies BELOW -- CL*)
  let c ~count agg ws = checked 
    { space         = ('c', agg, Cell.of_size A.width)
    ; doc           = "control and special"
    ; indexwidth    = indexwidth count
    ; indexlimit    = Some count
    ; widths        = ws
    ; classification = Fixed
    } 

  let vf agg ws =
    let doc = "virtual frame pointer" in
    checked { space          = ('V', agg, Cell.of_size A.width)
            ; doc            = doc
            ; indexwidth     = 1
            ; indexlimit     = Some 1
            ; widths         = ws
            ; classification = Fixed
            } 

  let x agg ws = checked 
    { space          = ('\000', agg, Cell.of_size A.width)
    ; doc            = "boxed rtls"
    ; indexwidth     = 1
    ; indexlimit     = Some 1
    ; widths         = ws
    ; classification = Reg
    } 

  let tempspace letter ~forspace doc agg w = checked 
    { space         = (letter, Rtl.Identity, Cell.of_size w);
      doc           = doc;
      indexwidth    = A.width;
      indexlimit    = None;
      widths        = [w];
      classification = Temp  
        { stands_for = stands_for forspace agg w;
          set_doc    = doc;
        };
    } 

  let t = tempspace 't' ~forspace:'r' "general-purpose temporaries"
  let u = tempspace 'u' ~forspace:'f' "floating-point temporaries"
  let v = tempspace 'v' ~forspace:'a' "address temporaries"
  let w = tempspace 'w' ~forspace:'p' "predicate temporaries"
  
  let s align = tempspace (char_of_int align) ~forspace:'m' 
      (string_of_int align ^ " aligned stack-slots temporaries")

  type 'a locations = 
    { pc:       'a
    ; npc:      'a
    ; cc:       'a
    ; fp_mode:  'a  (* FP rounding mode   *)
    ; fp_fcmp:  'a  (* FP condition codes *)
    }
  
  let locations ~c = match c with    
    |   { space       = ('c',bo,_) as space
        ; indexwidth  = iw 
        } ->
            let reg n = Rtl.reg (space, n, Rtl.C 1) in
            { pc      = reg 0
            ; npc     = reg 1
            ; cc      = reg 2 
            ; fp_mode = Rtl.slice 2 0 (reg 4)   
            ; fp_fcmp = Rtl.slice 2 0 (reg 5) 
            }
    |   { space = (s,_,_) } -> Impossible.impossible 
            ( "Standard locations from space " ^ Char.escaped s)
                                 
  
  let indices = { pc = 0; npc = 1; cc = 2; fp_mode = 4 ; fp_fcmp = 5 }
  let vfpcell = Cell.of_size A.width
  let vfp = Rtl.fetch (Rtl.reg (('V',Rtl.Identity,vfpcell), 0, Rtl.C 1)) A.width
end
module Standard32 = Standard(struct let width = 32 end)
module Standard64 = Standard(struct let width = 64 end)

@

