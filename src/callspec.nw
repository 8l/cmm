% -*- mode: Noweb; noweb-code-mode: caml-mode -*-
% vim:ts=8 sw=4 et:

% ------------------------------------------------------------------ 
\section{Constructing a Calling Convention}
% ------------------------------------------------------------------ 

A calling convention is represented by a [[Call.t]] value. Since this is
a quite complicated type, this module provides a client-oriented
interface to build such a value.

We provide a convenience function and two default values that represent whether the
caller or callee allocate/deallocate overflow blocks.
<<callspec.mli>>=
val overflow      : dealloc:Call.party -> alloc:Call.party -> Call.overflow
val c_overflow    : Call.overflow
val tail_overflow : Call.overflow
@ 
When a (general) register is spilled, a stack slot must be allocated. A
[[nvr_saver]] provides a slot for a non-volatile register.
<<callspec.mli>>=
type nvr_saver = Talloc.Multiple.t -> Register.t -> Rtl.loc
(* suggested by NR, but not used 
 * val save_nvrs: Space.t list -> nvr_saver
 *)
@
\paragraph{Return Address} The return address is the address a procedure
returns to. It needs to be saved accross calls if it is in a register.
We have two basic policies:

\begin{itemize}
\item [[KeepInPlace]] leaves the return address where it is, which is
      probably on the stack.
\item [[SaveToTemp c]] save the return address to a temporary of the
     space named~[[c]].
      Upon return, the return address is loaded into a fresh,
      short-lived temporary in the space with that name.
\end{itemize}

The basic policy cannot express to use coventional location for the
return address.
<<callspec.mli>>=
module ReturnAddress: sig
    type style  =
        | KeepInPlace           (* leave RA where it is     *)
        | SaveToTemp  of char   (* save RA in a temporary of this space  *)
    (* <<suggested functions>> *)
end     
@

NR suggested to provide the following functions. The problem is that an
implementation needs to know about which spaces to use for temporaries,
conventional return addresses and other informations not available.
They are currently unimplemented.

<<suggested functions>>=
val enter_in_loc    : Rtl.loc -> Block.t -> Rtl.exp 
val save_in_temp    : Rtl.exp -> Talloc.Multiple.t -> Rtl.loc
val save_as_is      : Rtl.exp -> Talloc.Multiple.t -> Rtl.loc
val exit_in_temp    : Rtl.exp -> Block.t -> Talloc.Multiple.t -> Rtl.loc
val exit_as_is      : Rtl.exp -> Block.t -> Talloc.Multiple.t -> Rtl.loc
@

\paragraph{The Specification} The following specification decribes a
calling convention. Passing it to [[to_call]], creates a [[Call.t]]
value.

<<callspec.mli>>=
<<type t>>


<<type t>>=
type t =
    { name              : string            (* name this CC *)
    ; stack_growth      : Memalloc.growth   (* up or down *)
    ; overflow          : Call.overflow     (* overflow handling *)
    ; memspace          : Register.space
    ; sp                : Register.t        (* stack pointer register *)
    ; sp_align          : int               (* alignment of sp *)
    ; all_regs          : Register.Set.t    (* regs visible to allocator *)
    ; nv_regs           : Register.Set.t    (* preserved registers *)
    ; save_nvr          : nvr_saver         (* how to save registers *)
    ; ra                : Rtl.loc           (* where is RA, how to treat it *)
                          * ReturnAddress.style
    }
@

A client has to supply the {\rtl}s for the [[cutto]] and alternate
return instructions.

<<callspec.mli>>=
val to_call: cutto:(unit, Mflow.cut_args) Target.map -> 
             return:(int -> int -> ra:Rtl.exp -> Rtl.rtl) -> 
             Automaton.cc_spec ->
             t -> Call.t   
@

% ------------------------------------------------------------------ 
\subsection{Implementation}
% ------------------------------------------------------------------ 

<<callspec.ml>>=
module R  = Rtl
module RU = Rtlutil
module W  = Rtlutil.Width
module T  = Talloc.Multiple
module C  = Call
module A  = Automaton

let overflow ~dealloc ~alloc = 
    { C.parameter_deallocator = dealloc
    ; C.result_allocator      = alloc
    }


let tail_overflow = 
    { C.parameter_deallocator = C.Callee
    ; C.result_allocator      = C.Caller
    }

let c_overflow =
    { C.parameter_deallocator = C.Caller
    ; C.result_allocator      = C.Caller
    } 

type nvr_saver = Talloc.Multiple.t -> Register.t -> Rtl.loc


module ReturnAddress = struct
    type style  =
        | KeepInPlace                   (* leave RA where it is     *)
        | SaveToTemp of char            (* save RA in a temporary   *)
end    

<<type t>>
@
We want to abstract from the direction of stack growth. Therefore, we
rather talking about the high and low end of a block we want to talk
about its young or old end.
<<callspec.ml>>=
let old_end wordsize growth block = match growth with
| Memalloc.Down -> RU.addk wordsize (Block.base block) (Block.size block)
| Memalloc.Up   -> Block.base block

let young_end wordsize growth block = match growth with
| Memalloc.Down -> Block.base block
| Memalloc.Up   -> RU.addk wordsize (Block.base block) (Block.size block) 


let wordsize t = match t.sp with ((_,_,ms),_,c) -> Cell.to_width ms c
let memspace t = t.memspace
let byteorder t = let (_, b, _) = t.memspace in b
let vfp wordsize             = Vfp.mk wordsize
let std_sp_location wordsize = 
    RU.add wordsize (vfp wordsize) (R.late "minus frame size" wordsize)

let mk_automaton wordsize memspace block_name automaton = fun () ->
    Block.srelative (vfp wordsize) block_name (A.at memspace)
      automaton
@

Small utilities to observe the return address location and its policy
independently.

<<callspec.ml>>=
let ra t    = fst t.ra
let style t = snd t.ra
@

The prolog is independent of the overflow policy. The first step is
always to allocate the frame for the procedure and to copy the incoming
parameters. Incoming overflow parameters always are held in a block
allocated by the caller.

<<callspec.ml>>=
let prolog t auto = 
  let autosp = (fun a -> young_end (wordsize t) t.stack_growth a.A.overflow) in
  C.incoming ~growth:t.stack_growth ~sp:(R.reg t.sp)
    ~mkauto:(mk_automaton (wordsize t) (memspace t) "in call parms" auto.A.call)
                        
    (* ~autosp:(fun _ -> vfp (wordsize t)) *)
    ~autosp:autosp
    ~postsp:(fun _ _ -> std_sp_location (wordsize t)) 
    ~insp:(fun a _ _ -> autosp a)
@

Before the epliog, [[sp]] points at the standard location, independent
of the overflow policy. Whereever the result is placed, it must be
deallocated by the caller and thus we cannot dealloate it. 

There is one problematic case: when the callee deallocates the
pamarameter block and the caller allocates the result block, is the
caller allowed to overlap the result with the parameter block?
Currently the compiler overlaps and thus we deallocate only up
to the young end of the result block. But in principle the result block
could be allocated outside the parameter block and thus we could
deallocate the parameter block completely. 

<<callspec.ml>>=
let epilog t auto =
    C.outgoing ~growth:t.stack_growth ~sp:(R.reg t.sp)
        ~mkauto:(mk_automaton (wordsize t) (memspace t) 
                   "out ovfl results" auto.A.results)
        ~autosp:(fun r -> std_sp_location (wordsize t))
        ~postsp:(match t.overflow with
                | {C.parameter_deallocator=C.Caller; C.result_allocator=C.Caller} ->
                    (fun _ _ -> vfp (wordsize t)) 
                | {C.parameter_deallocator=C.Callee; C.result_allocator=C.Caller} 
                | {C.parameter_deallocator=C.Caller; C.result_allocator=C.Callee} 
                | {C.parameter_deallocator=C.Callee; C.result_allocator=C.Callee} -> 
                    (fun a _ -> 
                        young_end (wordsize t) t.stack_growth a.A.overflow)
                )
@

A call always allocates space for outgoing parameters, independent of
who deallocates it.  Currently the space for results that the caller
allocates is overlapped with the block for outgoing parameters, so no
extra action is required. The young end of the outgoing block might or
might not be the standard location, depending on the stack layout
specified in Lua. Therefore, the move to the young end might be
redundant but we cannot test for it here.

<<callspec.ml>>=
let call_actuals t auto =
    C.outgoing ~growth:t.stack_growth ~sp:(R.reg t.sp)
        ~mkauto:(mk_automaton (wordsize t) (memspace t) "out call parms" auto.A.call)
        ~autosp:(fun r  -> std_sp_location (wordsize t))
        ~postsp:(fun a sp -> young_end (wordsize t) t.stack_growth a.A.overflow)
@

After a call the stack pointer points at the young end of the result
overflow block. We have to move it back to the standard location. This
might or might not be redundant. If it is not, this will deallocate any
callee-allocated result block.

<<callspec.ml>>=
let call_results t auto =
    let autosp = (fun a   -> young_end (wordsize t) t.stack_growth a.A.overflow) in
    C.incoming ~growth:t.stack_growth ~sp:(R.reg t.sp)
      ~mkauto:(mk_automaton (wordsize t) (memspace t) "in ovfl results" auto.A.results)
      ~autosp
      ~postsp:(fun _ _ -> std_sp_location (wordsize t)) 
      ~insp:(fun a _ _ -> autosp a)
@

Parameter passing for cuts is independent of parameter passing for
procedures.

<<callspec.ml>>=
let also_cuts_to t auto =
    let autosp = (fun r   -> std_sp_location (wordsize t)) in
    C.incoming ~growth:t.stack_growth ~sp:(R.reg t.sp)
      ~mkauto:(mk_automaton (wordsize t) (memspace t) "in cont parms" auto.A.cutto)
      ~autosp
      ~postsp:(fun _ _ -> std_sp_location (wordsize t)) 
      ~insp:(fun a _ _ -> autosp a)

let cut_actuals t auto base =
    C.outgoing ~growth:t.stack_growth ~sp:(R.reg t.sp) 
        ~mkauto:(fun ()  -> A.at (memspace t) ~start:base auto.A.cutto)
        ~autosp:(fun r   -> R.fetch (R.reg t.sp) (wordsize t))
        ~postsp:(fun _ _ -> R.fetch (R.reg t.sp) (wordsize t))

@

On entry, the return address is in the location specified by [[t]]. We
simply fetch it from there.

<<callspec.ml>>=
let ra_on_entry t block = R.fetch (ra t) (wordsize t)
@

Under the [[SaveToTemp]] policy we put the return address into a
\emph{fresh} temporary on exit. The hope is, that this temporary will be
short lived and not be spilled. Under the [[KeepInPlace]] policy, we
just keep the current location. The policy cannot express to put the
return address into a conventional location on exit, as required by the
Alpha for example.

\textbf{Alert:} The register allocator shows a bug when we put the
return address into a temporary on exit. 

<<callspec.ml>>=
let ra_on_exit t saved_ra block temp = match style t with
    | ReturnAddress.KeepInPlace  -> saved_ra 
    | ReturnAddress.SaveToTemp s -> Talloc.Multiple.loc temp s (wordsize t) 

let where_to_save_ra t = fun ra_on_entry temp -> match style t with
    | ReturnAddress.KeepInPlace  -> ra t
    | ReturnAddress.SaveToTemp s -> Talloc.Multiple.loc temp s (wordsize t) 

let to_call ~cutto ~return auto t =
    <<it's impossible for the stack pointer to be a useful register too>>;
    let volregs = Register.Set.diff t.all_regs t.nv_regs in
    { C.name           = t.name
    ; C.overflow_alloc = t.overflow
    ; C.call_parms     = {C.in'=prolog       t auto; C.out=call_actuals t auto}
    ; C.cut_parms      = {C.in'=also_cuts_to t auto; C.out=cut_actuals  t auto}
    ; C.results        = {C.in'=call_results t auto; C.out=epilog       t auto}
    
    ; C.stack_growth     = t.stack_growth
    ; C.stable_sp_loc    = std_sp_location (wordsize t)
    ; C.jump_tgt_reg     = Rtl.reg (Register.Set.choose volregs)
    ; C.replace_vfp      = Vfp.replace_with ~sp:(R.reg t.sp)
    ; C.sp_align         = t.sp_align
    ; C.ra_on_entry      = ra_on_entry t
    ; C.where_to_save_ra = where_to_save_ra t
    ; C.ra_on_exit       = ra_on_exit t
    ; C.sp_on_unwind     = (fun _   -> Rtl.null)
    ; C.sp_on_jump       = (fun _ _ -> Rtl.null)
    ; C.pre_nvregs       = t.nv_regs
    ; C.volregs          = volregs
    ; C.saved_nvr        = t.save_nvr
    ; C.return           = return
    }
<<it's impossible for the stack pointer to be a useful register too>>=
if Register.Set.mem t.sp t.all_regs then
  Impossible.impossible
    (Printf.sprintf "In convention \"%s\", stack pointer is also an ordinary register"
       t.name)
else
  ()
@


