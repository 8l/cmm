
% Grades     ::=    "%%Grades:" Quality Importance Urgency
% Quality    ::=    A|B|C|D|E
% Importance ::=    Central|Subsystem|Peripheral
% Urgency    ::=    Immediate|Soon|Later
%
% Please add grades for this module below. The line must start with % "%%".

%%Grades: B Peripheral Later

% ------------------------------------------------------------------ 
\section{Topological Sort}
% ------------------------------------------------------------------ 

Constants and types in {\PAL} can be declared recursively:  one
declaration may depend on several others.  This relationship must be
taken into account when constants and type declarations are evaluated
to find the meaning of each declared name.  On way is to find an order
such that a name's meaning is entered into an environment before any
other declaration that refers to it is processed.  Such an order is
called a \emph{topological order}.  The idea is to collect all
declarations of one kind, order them topologically, and process them. 
The [[Topsort]] module implements topological sorting for
declarations.

A type declaration ([[typedef]]) for a type $x$ can either refer to a
primitive type like bits8, or to another name $y$, which must be
defined by another [[typedef]].  In case $x$ refers to $y$, the type
declaration for $x$ depends on the declaration for $y$.  Bindings for
type-names in particular in the fat environment bind only primitive
types to names.  So the declaration for $y$ must be entered into the
environment before $x$ to resolve the meaning of $x$ with the help of
previously entered type declarations. The topological order of type
declarations with respect to their dependencies provides the right
order to process them.

Constant declaration ([[const]]) are similar to type declarations:  a
constant expression can refer to other constants which thus must be
processed first.  After the dependencies for constant declarations are
recorded, constant declarations can be topologically sorted. This
module provides an abstract view on declarations and their
dependencies and is not tailored towards a specific kind of
declaration to sort.

The topological sort faces some problems when dealing with
erroneous programs: 

\begin{itemize}
\item Usage of undeclared names.
\item Circular dependencies among declarations.
\item Multiple declarations of the same name.
\end{itemize}

All these problems must be reported as checked compile-time errors.
But we don't want to do this during sorting declarations but after
they are sorted and while processing them. However, each of the problems
mentioned above prevent a proper sorting. In order to be able to do
still much as work as possible in the presence of these problems we
make some assumptions and decisions:

\begin{itemize}
\item An undeclared name depends on no other declaration.
\item A name declared multiple times depends on the union of
      dependencies of its individual declarations.
\item A circular definitions is broken up by removing one declaration.
      This permits to sort the remaining declarations. 
\end{itemize}

For a list of circular declarations no topological order exists.  This
case is reported as an exception with a list of declarations that are
part of a cycle.  The client of this module then has to redo the
sorting with these declaration removed from the input.

This topological sort supports the case that one declaration may
declare multiple names.  For example, a [[typedef]] can define
multiple names for the same type.

% ------------------------------------------------------------------ 
\subsection{Interface}
% ------------------------------------------------------------------ 

The [[Topsort]] module is parameterized over the declarations it can
sort.  In case of circular declarations a list of offending
declarations is reported with a [[Cycle]] exception.  In the normal
case a list of declarationis is returned such that no declaration
depends on any later declaration in the list.  Or, stated positively,
all declarations depend only on their predecessors in the list.

<<signature S>>=
module type S = sig
    type decl
    exception Cycle of decl list
    val sort: decl list -> decl list (* raises Cycle *)
end
@

A declaration [[defines]] multiple names and may [[use]] several other
names in its body.  Aside from this, a declaration is abstract.

<<signature Sortable>>=
module type Sortable = sig
    type decl
    val defines : decl -> string list
    val uses    : decl -> string list
end
@

<<topsort.mli>>=
<<signature S>>
<<signature Sortable>>

module Make (S: Sortable) : (S with type decl = S.decl)
@

% ------------------------------------------------------------------ 
\subsection{Implementation}
% ------------------------------------------------------------------ 

The implementation uses a modified \emph{depth first search} for
topological sorting as it is outlined by Paulsen in \emph{{\small ML}
for the working Programmer} \cite{paulson:91:ml}.

<<topsort.ml>>=
<<signature S>>
<<signature Sortable>>

module ID       = struct type t=string let compare=compares end
module IDMap    = Map.Make(ID)

module Make (S: Sortable) = struct
    type decl = S.decl
    exception Cycle of decl list 

    <<functions>>
end
@

Multiple definitions can define the same name; this is an error but it
should not affect the sorting. A map points to all declarations that
declare that name.

<<functions>>=
let declmap decls =
    let find key  map    = try IDMap.find key map with Not_found -> [] in
    let add  d map key   = IDMap.add key (d :: find key map) map       in
    let add' map d       = List.fold_left (add d) map (S.defines d)    in
        List.fold_left add' IDMap.empty decls
@
The [[succ]] function calculates all declarations a given declaration
depends on with the help of [[S.uses]] and the previously calculated
[[map]]:  [[S.uses]] returns the names used by [[x]] and the [[map]]
translates these to the declarations for these names.
<<functions>>=
let succ map x =
    let find map n = try IDMap.find n map with Not_found -> [] in
    List.concat (List.map (find map) (S.uses x))
@
The map [[m]] contains for every name the declarations that define
this name.  Correct {\PAL} programs will use only one definition to
define a name.  This algorithm is difficult to understand -- please
refer to \emph{{\small ML} for the working Programmer}
\cite{paulson:91:ml} by Paulson for an explanation. 
<<functions>>=
let sort decls =
    let m             = declmap decls                               in
    let nv x (v,c)    = x::v ,c                                     in
    let rec sort todo path (visited, cycles) =
        match todo with
        | []    -> visited, cycles
        | x::xs ->
            let vc = if List.mem x path         then visited, x::cycles
                     else if List.mem x visited then visited, cycles
                     else nv x (sort (succ m x) (x::path) (visited,cycles))
                                                                    in
                sort xs path vc
                                                                    in
    match sort decls [] ([],[]) with
    | dd, [] -> List.rev dd
    | dd, cc -> raise (Cycle cc)
@



% ------------------------------------------------------------------ 
\subsection{Test}
% ------------------------------------------------------------------ 

The module [[Topsorttest]] provides a tiny example for declarations
from the \emph{{\small ML} for the working Programmer}.  It was used
interactively during development.

<<topsorttest.ml>>=
module D = struct
    type decl = string list * string list
    
    let defines (x,_) = x
    let uses    (_,y) = y
end

module DSort = Topsort.Make(D)

(*   DEFINED               USED BY DEFINITION *)
let regular =
    [ ["wake"]            , ["shower"; "eat" ]
    ; ["shower"]          , ["dress"] 
    ; ["dress"]           , ["go"]
    ; ["eat"]             , ["wash-up"]
    ; ["wash-up"]         , ["go"]
    ; ["go"]              , []
    ]
<<topsorttest.ml>>=
let multi_lhs1 =
    [ ["wake"]            , ["shower"; "eat" ]
    ; ["shower"; "bath"]  , ["dress"] 
    ; ["dress"]           , ["go"]
    ; ["eat"; "drink"]    , ["wash-up"]
    ; ["wash-up"]         , ["go"]
    ; ["go"]              , []
    ]
<<topsorttest.ml>>=
let multi_lhs2 =
    [ ["wake"]            , ["shower"; "eat" ]
    ; ["shower"; "bath"]  , ["dress"] 
    ; ["bath"]            , ["dress"] 
    ; ["dress"]           , ["go"]
    ; ["eat"; "drink"]    , ["wash-up"]
    ; ["wash-up"]         , ["go"]
    ; ["go"]              , []
    ; ["go"]              , []
    ]
<<topsorttest.ml>>=
let undefs =
    [ ["wake"]            , ["shower"; "eat" ]
    ; ["shower"; "bath"]  , ["dress"] 
    ; ["bath"]            , ["dress"] 
    ; ["dress"]           , ["go"]
    ; ["eat"; "drink"]    , ["wash-up"]
    ; ["wash-up"]         , ["go"]
    ]
<<topsorttest.ml>>=
let cycle1 =
    [ ["wake"]            , ["shower"; "eat" ]
    ; ["shower"; "bath"]  , ["dress"] 
    ; ["bath"]            , ["dress"] 
    ; ["dress"]           , ["go";"wake"]
    ; ["eat"; "drink"]    , ["wash-up"]
    ; ["wash-up"]         , ["go"]
    ; ["go"]              , []
    ; ["go"]              , []
    ]
@
